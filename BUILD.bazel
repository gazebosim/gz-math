load(
    "//ign_bazel:build_defs.bzl",
    "IGNITION_VISIBILITY",
    "cmake_configure_file",
    "generate_include_header",
    "ign_config_header",
    "ign_export_header",
)

package(
    default_visibility = IGNITION_VISIBILITY,
    features = [
        "-parse_headers",
        "-layering_check",
    ],
)

licenses(["notice"])

exports_files(["LICENSE"])

PROJECT_NAME = "ignition-math"

PROJECT_MAJOR = 6

PROJECT_MINOR = 6

PROJECT_PATCH = 0

# Generates config.hh based on the version numbers in CMake code.
ign_config_header(
    name = "config",
    src = "include/ignition/math/config.hh.in",
    cmakelists = ["CMakeLists.txt"],
    project_name = "ignition-math",
    project_version = (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),
)

ign_export_header(
    name = "include/ignition/math/Export.hh",
    export_base = "IGNITION_MATH",
    lib_name = "ignition-math",
    visibility = ["//visibility:private"],
)

public_headers_no_gen = [
    "include/ignition/math/Angle.hh",
    "include/ignition/math/AxisAlignedBox.hh",
    "include/ignition/math/Box.hh",
    "include/ignition/math/Color.hh",
    "include/ignition/math/Cylinder.hh",
    "include/ignition/math/DiffDriveOdometry.hh",
    "include/ignition/math/Filter.hh",
    "include/ignition/math/Frustum.hh",
    "include/ignition/math/GaussMarkovProcess.hh",
    "include/ignition/math/Helpers.hh",
    "include/ignition/math/Inertial.hh",
    "include/ignition/math/Kmeans.hh",
    "include/ignition/math/Line2.hh",
    "include/ignition/math/Line3.hh",
    "include/ignition/math/MassMatrix3.hh",
    "include/ignition/math/Material.hh",
    "include/ignition/math/MaterialType.hh",
    "include/ignition/math/Matrix3.hh",
    "include/ignition/math/Matrix4.hh",
    "include/ignition/math/MovingWindowFilter.hh",
    "include/ignition/math/OrientedBox.hh",
    "include/ignition/math/PID.hh",
    "include/ignition/math/Plane.hh",
    "include/ignition/math/Pose3.hh",
    "include/ignition/math/Quaternion.hh",
    "include/ignition/math/Rand.hh",
    "include/ignition/math/RollingMean.hh",
    "include/ignition/math/RotationSpline.hh",
    "include/ignition/math/SemanticVersion.hh",
    "include/ignition/math/SignalStats.hh",
    "include/ignition/math/Sphere.hh",
    "include/ignition/math/SphericalCoordinates.hh",
    "include/ignition/math/Spline.hh",
    "include/ignition/math/Stopwatch.hh",
    "include/ignition/math/Temperature.hh",
    "include/ignition/math/Triangle.hh",
    "include/ignition/math/Triangle3.hh",
    "include/ignition/math/Vector2.hh",
    "include/ignition/math/Vector3.hh",
    "include/ignition/math/Vector3Stats.hh",
    "include/ignition/math/Vector4.hh",
    "include/ignition/math/detail/Box.hh",
    "include/ignition/math/detail/Cylinder.hh",
    "include/ignition/math/detail/Sphere.hh",
    "include/ignition/math/graph/Edge.hh",
    "include/ignition/math/graph/Graph.hh",
    "include/ignition/math/graph/GraphAlgorithms.hh",
    "include/ignition/math/graph/Vertex.hh",
]

private_headers = [
    "src/FrustumPrivate.hh",
    "src/KmeansPrivate.hh",
    "src/MaterialType.hh",
    "src/RotationSplinePrivate.hh",
    "src/SignalStatsPrivate.hh",
    "src/SplinePrivate.hh",
    "src/Vector3StatsPrivate.hh",
]

generate_include_header(
    name = "mathhh_genrule",
    out = "include/ignition/math.hh",
    hdrs = public_headers_no_gen + [
        "include/ignition/math/config.hh",
        "include/ignition/math/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/ignition/math/config.hh",
    "include/ignition/math/Export.hh",
    "include/ignition/math.hh",
]

cc_library(
    name = "ign_math",
    srcs = [
        "src/Angle.cc",
        "src/AxisAlignedBox.cc",
        "src/Color.cc",
        "src/DiffDriveOdometry.cc",
        "src/Frustum.cc",
        "src/GaussMarkovProcess.cc",
        "src/Helpers.cc",
        "src/Kmeans.cc",
        "src/Material.cc",
        "src/PID.cc",
        "src/Rand.cc",
        "src/RollingMean.cc",
        "src/RotationSpline.cc",
        "src/RotationSplinePrivate.cc",
        "src/SemanticVersion.cc",
        "src/SignalStats.cc",
        "src/SphericalCoordinates.cc",
        "src/Spline.cc",
        "src/SplinePrivate.cc",
        "src/Stopwatch.cc",
        "src/Temperature.cc",
        "src/Vector3Stats.cc",
    ] + private_headers,
    hdrs = public_headers,
    includes = ["include"],
)

# use shared library only when absolutely needd
cc_binary(
    name = "libignition-math6.so",
    includes = ["include"],
    linkopts = ["-Wl,-soname,libignition-math6.so"],
    linkshared = True,
    deps = [
        ":ign_math",
    ],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    deps = [
        ":ign_math",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in glob(
    [
        "src/*_TEST.cc",
        "src/graph/*_TEST.cc",
    ],
)]
