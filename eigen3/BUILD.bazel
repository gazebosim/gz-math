load("@rules_license//rules:license.bzl", "license")

package(
    default_applicable_licenses = ["//:license"],
    features = [
        "layering_check",
        "parse_headers",
    ],
)

license(
    name = "license",
    package_name = "gz-math-eigen3",
)

public_headers = glob(
    include = [
        "include/gz/math/eigen3/*.hh",
    ],
)

private_headers = glob(
    include = ["src/*.hh"],
)

sources = glob(
    include = [
        "src/*.cc",
    ],
    exclude = [
        "src/*_TEST.cc",
    ],
)

cc_library(
    name = "eigen3",
    srcs = sources + private_headers,
    hdrs = public_headers,
    includes = ["include"],
    visibility = ["//visibility:public"],
    deps = [
        "//:math",
        "@eigen",
    ],
)

test_sources = glob(
    include = [
        "src/*_TEST.cc",
    ],
)

[
    cc_test(
        name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
        srcs = [src],
        deps = [
            ":eigen3",
            "@googletest//:gtest",
            "@googletest//:gtest_main",
        ],
    )
    for src in test_sources
]
